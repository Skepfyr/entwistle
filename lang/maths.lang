Module:
  (_ Statement _ nl | _ nl)*

Statement:
  Ident _ '=' _ Expression
  Expression

== Statement
a = b
==
Statement:
  Ident: "a"
  " "
  "="
  " "
  Expression:
    Ident: "b"
==

Expression:
  Expression _ ('+' | '-') _ ~Expression
  Expression _ ('*' | '/') _ ~Expression
  ('+' | '-') Expression
  Expression '!'
  '(' _ $Expression _ ')'
  Ident '(' CommaSeparated<$Expression> ')'
  Number
  Ident

== Expression
1 + 2 * 3 + -4.0
==
Expression:
  Expression:
    Expression:
      Number: "1"
    " "
    "+"
    " "
    Expression:
      Expression:
        Number: "2"
      " "
      "*"
      " "
      Expression:
        Number: "3"
  " "
  "+"
  " "
  Expression:
    "-"
    Expression:
      Number: "4.0"
==

== Expression
log(a, 10)
==
Expression:
  Ident: "log"
  "("
  Expression:
    Ident: "a"
  ""
  ","
  " "
  Expression:
    Number: "10"
  ")"
==

-CommaSeparated<Item>: Item (_ "," _ Item)? (_ ",")?

@Number: /\d[\d_]*(.\d+)?/
@Ident: /[[:alpha:]][\w_]*/

== Number
3.14159265
==
Number: "3.14159265"
==

-@_: /\s+/?
-@nl: '\n'
