Module:
  (_ Statement _ nl | _ nl)*

Statement:
  Variable _ '=' _ Expression
  Expression

## Statement
a = b
##
Statement:
  Variable: "a"
  "="
  Expression:
    Variable: "b"
##

Expression:
  Expression _ ('+' | '-') _ ~Expression
  Expression _ ('*' | '/') _ ~Expression
  ('+' | '-') Expression
  Expression '!'
  '(' _ $Expression _ ')'
  Number
  Variable

## Expression
1 + 2 * 3 + -4.0
##
Expression:
  Expression:
    Expression:
      Number: "1"
    "+"
    Expression:
      Expression:
        Number: "2"
      "*"
      Expression:
        Number: "3"
  "+"
  Expression:
    "-"
    Expression:
      Number: "4.0"
##

@Number: Digit (Digit | '_')* ('.' Digit+)?
Digit: '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9'
@Variable: Letter (Letter | Digit | '_')*
Letter: LowerLetter | UpperLetter
LowerLetter: 'a' | 'b' | 'c' | 'd' | 'e' | 'f' | 'g' | 'h' | 'i' | 'j' | 'k' | 'l' | 'm' | 'n' | 'o' | 'p' | 'q' | 'r' | 's' | 't' | 'u' | 'v' | 'w' | 'x' | 'y' | 'z'
UpperLetter: 'A' | 'B' | 'C' | 'D' | 'E' | 'F' | 'G' | 'H' | 'I' | 'J' | 'K' | 'L' | 'M' | 'N' | 'O' | 'P' | 'Q' | 'R' | 'S' | 'T' | 'U' | 'V' | 'W' | 'X' | 'Y' | 'Z'

## Number
3.14159265
##
Number: "3.14159265"
##

-@_: ' '?
-@nl: '\n'
