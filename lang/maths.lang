Module:
  (_ Statement _ nl | _ nl)*

Statement:
  Ident _ '=' _ Expression
  Expression

== Statement
a = b
==
Statement:
  Ident: "a"
  _:
    " "
  "="
  _:
    " "
  Expression:
    Ident: "b"
==

Expression:
  Expression _ ('+' | '-') _ ~Expression
  Expression _ ('*' | '/') _ ~Expression
  ('+' | '-') Expression
  Expression '!'
  '(' _ $Expression _ ')'
  Ident _ '(' _ CommaSeparated<$Expression> _ ')'
  Number
  Ident

== Expression
1 + 2 * 3 + -4.0
==
Expression:
  Expression:
    Expression:
      Number: "1"
    _:
      " "
    "+"
    _:
      " "
    Expression:
      Expression:
        Number: "2"
      _:
        " "
      "*"
      _:
        " "
      Expression:
        Number: "3"
  _:
    " "
  "+"
  _:
    " "
  Expression:
    "-"
    Expression:
      Number: "4.0"
==

== Expression
log(a, 10)
==
Expression:
  Ident: "log"
  _:
  "("
  _:
  Expression:
    Ident: "a"
  _:
  ","
  _:
    " "
  Expression:
    Number: "10"
  _:
  ")"
==

-CommaSeparated<Item>: Item (_ "," _ Item)? (_ ",")?

@Number: /\d[\d_]*(.\d+)?/
@Ident: /[[:alpha:]][\w_]*/

== Number
3.14159265
==
Number: "3.14159265"
==

_: whitespace?
-@whitespace: /\s+/
-@nl: '\n'
