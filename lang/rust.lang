@as: /as\b/
@break: /break\b/
@const: /const\b/
@continue: /continue\b/
@crate: /crate\b/
@else: /else\b/
@enum: /enum\b/
@extern: /extern\b/
@false: /false\b/
@fn: /fn\b/
@for: /for\b/
@if: /if\b/
@impl: /impl\b/
@in: /in\b/
@let: /let\b/
@loop: /loop\b/
@match: /match\b/
@mod: /mod\b/
@move: /move\b/
@mut: /mut\b/
@pub: /pub\b/
@ref: /ref\b/
@return: /return\b/
@self: /self\b/
@Self: /Self\b/
@static: /static\b/
@struct: /struct\b/
@super: /super\b/
@trait: /trait\b/
@true: /true\b/
@type: /type\b/
@unsafe: /unsafe\b/
@use: /use\b/
@where: /where\b/
@while: /while\b/
@async: /async\b/
@await: /await\b/
@dyn: /dyn\b/

@abstract: /abstract\b/
@become: /become\b/
@box: /box\b/
@do: /do\b/
@final: /final\b/
@macro: /macro\b/
@override: /override\b/
@priv: /priv\b/
@typeof: /typeof\b/
@unsized: /unsized\b/
@virtual: /virtual\b/
@yield: /yield\b/
@try: /try\b/

@union: /union\b/
@StaticLifetime: /'static\b/

@StrictKeyword:
  as
  break
  const
  continue
  crate
  else
  enum
  extern
  false
  fn
  for
  if
  impl
  in
  let
  loop
  match
  mod
  move
  mut
  pub
  ref
  return
  self
  Self
  static
  struct
  super
  trait
  true
  type
  unsafe
  use
  where
  while
  async
  await
  dyn

@ReservedKeyword:
  abstract
  become
  box
  do
  final
  macro
  override
  priv
  typeOf
  unsized
  virtual
  yield
  try

@WeakKeyword:
  union
  StaticLifetime

@IdentifierOrKeyword: /[a-zA-Z][a-zA-Z0-9_]*|_[a-zA-Z0-9_]+\b/

@RawIdentifierName: (!>> crate | self | super | Self) IdentifierOrKeyword

@RawIdentifier: "r#" RawIdentifierName

@NonKeywordIdentifier: (!>> StrictKeyword | ReservedKeyword) IdentifierOrKeyword

@Identifier:
  NonKeywordIdentifier
  RawIdentifier

# TODO: think about comments
-_:
  Whitespace+
  Whitespace* Comment Whitespace*

@Whitespace: /\s/

@EscapedNewline: r"\n"
@EscapedCarriageReturn: r"\r"
@EscapedTab: r"\t"
@EscapedBackslash: r"\\"
@EscapedNull: r"\0"
@EscapedSingleQuote: r"\'"
@EscapedDoubleQuote: r#"\""#
@EscapedAsciiChar: /\\x[0-7][0-9a-fA-F]/
@EscapedByte: /\\x[0-9a-fA-F]{2}/
@EscapedUnicodeChar: /\\u\{([0-9a-fA-F]_*){1,6}\}/

-@AsciiEscape:
  EscapedAsciiChar
  EscapedNewline
  EscapedCarriageReturn
  EscapedTab
  EscapedBackslash
  EscapedNull

-@ByteEscape:
  EscapedByte
  EscapedNewline
  EscapedCarriageReturn
  EscapedTab
  EscapedBackslash
  EscapedNull

-@UnicodeEscape:
  EscapedUnicodeChar

-@QuoteEscape:
  EscapedSingleQuote
  EscapedDoubleQuote

@Newline: "\n" | "\r\n"
@StringContinue: "\\" Newline

@StringChar:
  QuoteEscape
  AsciiEscape
  UnicodeEscape
  /\P{C}/
@ByteChar:
  QuoteEscape
  ByteEscape
  /[ -~]/

-@NonSingleQuoteStringChar: (!>> "'") StringChar
Character: "'" NonSingleQuoteStringChar "'"

-@NonDoubleQuoteStringChar: (!>> '"') (StringChar | StringContinue)
String: r#"""# NonDoubleQuoteStringChar* r#"""#

@RawStringContents<Hashes>: (!>> Hashes) /\P{C}/
RawString: /r#{N}"/ RawStringContents</#{N}/> /"#{N}/


-@NonSingleQuoteByteChar: (!>> "'") ByteChar
Byte: "b'" NonSingleQuoteByteChar "'"

-@NonDoubleQuoteByteChar: (!>>'"') ByteChar
ByteString: 'b"'# NonDoubleQuoteByteChar* '"'

@RawByteStringContents<Hashes>: (!>> Hashes) /[ -~]/
RawByteString: /br#{N}"/ RawStringContents</#{N}/> /"#{N}/

@Integer:
  /[0-9_]+/
  /0x[0-9a-fA-F_]+/
  /0o[0-7_]+/
  /0b[01_]+/

@Float: /[0-9_]+(.[0-9_]+)?((e|E)(+|-)[0-9_]+)?/

@Number: Integer | Float

TupleIndex: Integer

@Bool: "true" | "false"

@LifetimeToken: "'_" | "'" IdentifierOrKeyword
@Lifetime: "'" NonKeywordIdentifier
@Label: "'" NonKeywordIdentifier

# TODO: Macros?

Crate: Utf8Bom? (Shebang Newline)? _ (InnerAttribute _)* (Item _)*

@Utf8Bom: "\uFEFF"
@Shebang: "#!" Whitespace* BlockComment? Whitespace* (/[^\[\n\r]/ /[^\n\r]/+)?

-CommaSeparated<Item>: Item (_ "," _ Item)? (_ ",")?

ConfigurationPredicate:
  ConfigurationOption
  ConfigurationAll
  ConfigurationAny
  ConfigurationNot

ConfigurationOption: Identifier ("=" (String | RawString))?
ConfigurationAll: "all(" ConfigurationPredicateList? ")"
ConfigurationAny: "any(" ConfigurationPredicateList? ")"
ConfigurationNot: "not(" ConfigurationPredicateList? ")"
ConfigurationPredicateList: CommaSeparated<ConfigurationPredicate>

CfgAttribute: "cfg(" ConfigurationPredicate ")"
CfgAttrAttribute: "cfg_attr(" _ ConfigurationPredicate _ "," _ CfgAttrs? _ ")"
CfgAttrs: CommaSeparated<Attr>

Item: (OuterAttribute _)* (VisItem | MacroItem)
VisItem: Visibility InnerItem
-InnerItem:
  Module
  ExternCrate
  UseDeclaration
  Function
  TypeAlias
  Struct
  Enumeration
  Union
  ConstantItem
  StaticItem
  Trait
  Implementation
  ExternBlock

MacroItem: MacroInvocationSemi | MacroRulesDefinition

Module:
  unsafe? _ mod _ Identifier _ ";"
  unsafe? _ mod _ Identifier "{" _ (InnerAttribute _)* (Item _)* "}"

ExternCrate: extern _ crate _ CrateRef (_ AsClause)? _ ";"
CrateRef: Identifier | self
AsClause: as _ (Identifier | "_")

UseDeclaration: use _ UseTree _ ";"
UseTree:
  ((SimplePath _)? "::" _)? "*"
  ((SimplePath _)? "::" _)? "{" _ CommaSeparated<UseTree>? _ "}"
  SimplePath (_ as _ (Identifier | "_"))?

Function: FunctionQualifiers _ fn _ Identifier _ GenericParams? "(" _ FunctionParameters? _ ")" _ (FunctionReturnType _)? WhereClause? (BlockExpression | ";")

FunctionQualifiers: (const _)? (async _)? (unsafe _)? (extern _ Abi? _)?
Abi: String | RawString

FunctionParameters:
  SelfParam _ ","? _
  (SelfParam _ "," _)? CommaSeparated<FunctionParam>

SelfParam: (OuterAttribute _)* (ShorthandSelf | TypedSelf)
ShorthandSelf: ("&" Lifetime?)? _ (mut _)? self
TypedSelf: (mut _)? self _ ":" _ Type

FunctionParam: (OuterAttribute _)* (FunctionParamPattern | "...")
FunctionParamPattern: Pattern _ ":" _ (Type | "...")

FunctionReturnType: "->" _ Type

TypeAlias: type _ Identifier _ GenericParams? _ WhereClause? "=" _ Type _ ";"

Struct: StructStruct | TupleStruct
StructStruct: struct _ Identifier _ GenericParams? WhereClause? ("{" _ (StructFields? _) "}" | ";")
TupleStruct: struct _ Identifier _ GenericParams? "(" _ TupleFields? ")" WhereClause? ";"

StructFields: CommaSeparated<StructField>
StructField: OuterAttribute* Visibility? Identifier ":" _ Type

TupleFields: CommaSeparated<TupleField>
TupleField: OuterAttribute* Visibility? Type

Enumeration: enum _ Identifier _ GenericParams? _ WhereClause? _ "{" _ EnumItems? _ "}"
EnumItems: CommaSeparated<EnumItem>
EnumItem: OuterAttribute* _ Visibility? _ Identifier _ (EnumItemTuple | EnumItemStruct | EnumItemDiscriminant)?
EnumItemTuple: "(" _ TupleFields? _ ")"
EnumItemStruct: "{" _ StructFields? _ "}"
EnumItemDiscriminant: "=" Expression

Union: union _ Identifier _ GenericParams? WhereClause? "{" _ StructFields _ "}"

ConstantItem: const _ (Identifier | "_") _ ":" _ Type _ ("=" _ Expression _)? ";"
StaticItem: static _ (mut _)? Identifier _ ":" _ Type _ ("=" _ Expression _)? ";"

Trait: (unsafe _)? trait _ Identifier _ GenericParams? (":" _ TypeParamBounds?)? WhereClause? "{" InnerAttribute* AssociatedItem* "}"
Implementation: InherentImpl | TraitImpl
InherentImpl: impl _ GenericParams? Type _ WhereClause? "{" InnerAttribute* AssociatedItem* "}"
TraitImpl: (unsafe _)? impl _ GenericParams? "!"? _ TypePath _ for _ Type _ WhereClause? "{" InnerAttribute* AssociatedItem* "}"

ExternBlock: (unsafe _)? extern _ Abi? _ "{" InnerAttribute* ExternalItem* "}"
ExternalItem: OuterAttribute* (MacroInvocationSemi | ((Visibility _)? (StaticItem | Function)))

GenericParams: "<" CommaSeparated<GenericParam>? ">"
GenericParam: OuterAttribute* (LifetimeParam | TypeParam | ConstParam)
LifetimeParam: Lifetime (_ ":" _ LifetimeBounds)?
TypeParam: Identifier (_ ":" _ TypeParamBounds?)? (_ "=" _ Type)?
ConstParam: const _ Identifier _ ":" _ Type

WhereClause: where _ CommaSeparated<WhereClauseItem>
WhereClauseItem: LifetimeWhereClauseItem | TypeBoundWhereClauseItem
LifetimeWhereClauseItem: Lifetime _ ":" _ LifetimeBounds
TypeBoundWhereClauseItem: (ForLifetimes _)? Type _ ":" (_ TypeParamBounds)?
ForLifetimes: for _ GenericParams

AssociatedItem: OuterAttribute* (MacroInvocationSemi | ((Visibility _)? (TypeAlias | ConstantItem | Function)))


InnerAttribute: "#![" _ Attr _ "]"
OuterAttribute: "#[" _ Attr _ "]"
Attr: SimplePath (_ AttrInput)?
AttrInput: DelimTokenTree | "=" _ LiteralExpression

MetaItem:
  SimplePath
  SimplePath _ "=" _ LiteralExpression
  SimplePath "(" MetaSeq? ")"
MetaSeq: CommaSeparated<MetaItemInner>
MetaItemInner: MetaItem | LiteralExpression

MetaWord: Identifier
MetaNameValueStr: Identifier _ "=" _ (String | RawString)
MetaListPaths: Identifier _ "(" CommaSeparated<SimplePath> ")"
MetaListIdents: Identifier _ "(" CommaSeparated<Identifier> ")"
MetaListNameValueStr: Identifier _ "(" CommaSeparated<MetaNameValueStr> ")"



Statement:
  ";"
  Item
  LetStatement
  ExpressionStatement
  MacroInvocationSemi
LetStatement: OuterAttribute* let _ Pattern _ (":" _ Type _)? ("=" _ Expression)? ";"
ExpressionStatement:
  ExpressionWithoutBlock ";"
  ExpressionWithBlock ";"?

Expression: OuterAttribute* (ExpressionWithoutBlock | ExpressionWithBlock)
ExpressionWithoutBlock:
  (ReturnExpression | BreakExpression | ContinueExpression | ClosureExpression)

  ~ExpressionWithoutBlock _ ("=" | "+=" | "-=" | "*=" | "/=" | "%=" | "&=" | "|=" | "^=" | "<<=" | ">>=") _ ExpressionWithoutBlock

  # Range expressions
  ~ExpressionWithoutBlock _ ".." _ ~ExpressionWithoutBlock | ~ExpressionWithoutBlock _ ".." | ".." _ ~ExpressionWithoutBlock | ".." | ~ExpressionWithoutBlock _ "..=" _ ~ExpressionWithoutBlock | "..=" _ ~ExpressionWithoutBlock

  # Operator expressions
  ExpressionWithoutBlock _ "||" _ ~ExpressionWithoutBlock
  ExpressionWithoutBlock _ "&&" _ ~ExpressionWithoutBlock
  ExpressionWithoutBlock _ ("==" | "!=" | ">" | "<" | ">=" | "<=") _ ~ExpressionWithoutBlock
  ExpressionWithoutBlock _ "|" _ ~ExpressionWithoutBlock
  ExpressionWithoutBlock _ "^" _ ~ExpressionWithoutBlock
  ExpressionWithoutBlock _ "&" _ ~ExpressionWithoutBlock
  ExpressionWithoutBlock _ ("<<" | ">>") _ ~ExpressionWithoutBlock
  ExpressionWithoutBlock _ ("*" | "/" | "%") _ ~ExpressionWithoutBlock
  ExpressionWithoutBlock _ ("+" | "-") _ ~ExpressionWithoutBlock
  ExpressionWithoutBlock _ as _ TypeNoBounds
  "&" _ (mut _)? ExpressionWithoutBlock
  "*" _ ExpressionWithoutBlock
  ("-" | "!") _ ExpressionWithoutBlock
  ExpressionWithoutBlock _ "?"

  ExpressionWithoutBlock _ "." _ await
  ExpressionWithoutBlock _ "[" _ $ExpressionWithoutBlock _ "]"
  ExpressionWithoutBlock _ "." _ TupleIndex

  ArrayExpression
  TupleExpression
  StructExpression
  EnumerationVariantExpression

  ExpressionWithoutBlock _ "." _ Identifier
  ExpressionWithoutBlock _ "." _ PathExprSegment _ "(" (CallParams _)? ")"
  ExpressionWithoutBlock _ "(" _ (CallParams _)? ")"
  PathExpression
  GroupedExpression
  LiteralExpression
  MacroInvocation

ExpressionWithBlock:
  BlockExpression
  AsyncBlockExpression
  UnsafeBlockExpression
  LoopExpression
  IfExpression
  IfLetExpression
  MatchExpression

LiteralExpression: Char | String | RawString | Byte | ByteString | Number | Bool
PathExpression: PathInExpression | QualifiedPathInExpression
BlockExpression: "{" _ (InnerAttribute _)* Statements _ "}"
Statements:
  (Statement _)+
  (Statement _)+ ExpressionWithoutBlock
  ExpressionWithoutBlock
AsyncBlockExpression: async (_ move)? _ BlockExpression
UnsafeBlockExpression: unsafe _ BlockExpression

GroupedExpression: "(" _ (InnerAttribute _)* Expression _ ")"
ArrayExpression: "[" _ (InnerAttribute _)* (ArrayElements _)? "]"
ArrayElements:
  CommaSeparated<Expression>
  Expression _ ";" _ Expression
TupleExpression: "(" _ (InnerAttribute _)* (ArrayElements _)? ")"
TupleElements: CommaSeparated<Expression>
StructExpression:
  StructExprStruct
  StructExprTuple
  StructExprUnit
StructExprStruct: PathInExpression _ "{" _ (InnerAttribute _)* StructExprFields _ "}"
StructExprFields: (StructExprField _ "," _)* (StructField (_ ",")? | StructBase?)
StructExprField:
  Identifier
  (Identifier | TupleIndex) _ ":" _ Expression
StructBase: ".." _ Expression
StructExprTuple: PathInExpression _ "(" _ (InnerAttribute _)* (CommaSeparated<Expression> _)? ")"
StructExprUnit: PathInExpression

EnumerationVariantExpression:
  EnumExprStruct
  EnumExprTuple


  EnumExprUnit
EnumExprStruct: PathInExpression _ "{" _ EnumExprFields _ "}"
EnumExprFields: CommaSeparated<EnumExprField>
EnumExprField:
  Identifier
  (Identifier | TupleIndex) _ ":" _ Expression
EnumExprTuple: PathInExpression _ "(" _ (CommaSeparated<Expression> _)? ")"
EnumExprUnit: PathInExpression

CallParams: CommaSeparated<Expression>

ClosureExpression: (move _)? "|" _ ClosureParameters? _ "|" _ (Expression | "->" _ TypeNoBounds _ BlockExpression)
ClosureParameters: CommaSeparated<ClosureParam>
ClosureParam: (OuterAttribute _)* Pattern (_ ":" _ Type)?

LoopExpression: (LoopLabel _)? (InfiniteLoopExpression | PredicateLoopExpression | PredicatePatternLoopExpression | IteratorLoopExpression)
LoopLabel: Label _ ":"
InfiniteLoopExpression: loop _ BlockExpression
PredicateLoopExpression: while _ Expression _ BlockExpression
# TODO: This expression shouldn't be able to be a struct or lazy boolean
PredicatePatternLoopExpression: while _ let _ MatchArmPatterns "=" Expression _ BlockExpression
IteratorLoopExpression: for _ Pattern _ in _ Expression _ BlockExpression

BreakExpression: break (_ Label)? (_ Expression)?
ContinueExpression: continue (_ Label)?

IfExpression: if _ Expression _ BlockExpression (_ else _ (BlockExpression | IfExpression | IfLetExpression))?
# TODO: This expression shouldn't be able to be a struct or lazy boolean
IfLetExpression: if _ let _ MatchArmPatterns "=" Expression _ BlockExpression (_ else _ (BlockExpression | IfExpression | IfLetExpression))?

MatchExpression: match _ Expression _ "{" _ (InnerAttribute _)* (MatchArms _)? "}"
MatchArms: (MatchArm _ "=>" (ExpressionWithoutBlock _ "," | ExpressionWithBlock (_ ",")?) _)* MatchArm _ "=>" _ Expression (_ ",")?
MatchArm: (OuterAttribute _)* MatchArmPatterns _ (MatchArmGuard _)?
MatchArmPatterns: ("|" _)? Pattern (_ "|" _ Pattern)*
MatchArmGuard: if _ Expression

ReturnExpression: return (_ Expression)?
